//----------------------------------------------------------------------------
//-- Template based on the measures from article:
//--	https://www.daxpatterns.com/time-patterns/
//--    https://www.sqlbi.com/articles/userelationship-in-calculated-columns/
//--    https://dash-intel.com/powerbi/modeling_date_functions.php
//--	Upated for Power BI syntax 
//--	and modified to allow for Search/replace the name of:
//--		<DATE TABLE> = Calendar
//--		<FactTable> = FactInternetSales
//-- 			<Measure> = Sales
//--		<SemiAdditiveFact>
//--			<SemiAddditiveMeasure>
//--
//--  based on 'FactInternetSales'[<BaseMeasure>]
//----------------------------------------------------------------------------
//--  Create a calculated SUM() measure over the FactTable/BaseMeasure:
//--	Sales Amount  =  SUM('FactInternetSales'[<BaseMeasure>])
//----------------------------------------------------------------------------

//  To do: Add Moving average template
//	Period to date measures
//	Update the Calendar Dimension M Script to have [Month Abbv] and change [Month] to [MonthName]


//----------------------------------------------------------------------------
// DAX Calculated columns needed in the Calendar/date dimension
// Dependent on the generic Calendar dimension M Script for Power BI
// Note: some of these can be accomplished im M as well. DAX version for reference
//----------------------------------------------------------------------------

YearMonthNumber = ('Calendar'[YearNumber] - MIN( 'Calendar'[YearNumber] )) * 12 + 'Calendar'[MonthNumber]

YearQuarterNumber = ( 'Calendar'[YearNumber] - MIN ( 'Calendar'[YearNumber] ) ) * 4 + 'Calendar'[QuarterNumber]

MonthDayNumber = 
    DAY(
        'Calendar'[Date]
    )

PM Date =
CALCULATE (
    MAX  ( 'Calendar'[Date] ),
    ALL ( 'Calendar' ),
    FILTER (
        ALL ( 'Calendar'[MonthDayNumber] ),
        'Calendar'[MonthDayNumber] <= EARLIER ( 'Calendar'[MonthDayNumber] )
            || EARLIER ( 'Calendar'[MonthDayNumber] ) = EARLIER ( 'Calendar'[MonthDays] )
    ),
    FILTER (
        ALL ( 'Calendar'[YearMonthNumber] ),
        'Calendar'[YearMonthNumber]
            = EARLIER ( 'Calendar'[YearMonthNumber] ) - 1
    )
)

PQ Date =
CALCULATE (
    MAX  ( 'Calendar'[Date] ),
    ALL ( 'Calendar' ),
    FILTER (
        ALL ( 'Calendar'[MonthDayNumber] ),
        'Calendar'[MonthDayNumber] <= EARLIER ( 'Calendar'[MonthDayNumber] )
            || EARLIER ( 'Calendar'[MonthDayNumber] ) = EARLIER ( 'Calendar'[MonthDays] )
    ),
    FILTER (
        ALL ( 'Calendar'[YearMonthNumber] ),
        'Calendar'[YearMonthNumber]
            = EARLIER ( 'Calendar'[YearMonthNumber] ) - 3
    )
)

PY Date =
CALCULATE (
    MAX  ( 'Calendar'[Date] ),
    ALL ( 'Calendar' ),
    FILTER (
        ALL ( 'Calendar'[MonthDayNumber] ),
        'Calendar'[MonthDayNumber] <= EARLIER ( 'Calendar'[MonthDayNumber] )
            || EARLIER ( 'Calendar'[MonthDayNumber] ) = EARLIER ( 'Calendar'[MonthDays] )
    ),
    FILTER (
        ALL ( 'Calendar'[YearMonthNumber] ),
        'Calendar'[YearMonthNumber]
            = EARLIER ( 'Calendar'[YearMonthNumber] ) - 12
    )
)

YearDayNumber =
COUNTROWS (
    FILTER (
        ALL ( 'Calendar' ),
        'Calendar'[Date] <= EARLIER ( 'Calendar'[Date] )
            && 'Calendar'[YearNumber] = EARLIER ( 'Calendar'[YearNumber] )
    )
)

QuarterDayNumber =
COUNTROWS (
    FILTER (
        ALL ( 'Calendar' ),
        'Calendar'[Date] <= EARLIER ( 'Calendar'[Date] )
            && 'Calendar'[YearNumber] = EARLIER ( 'Calendar'[YearNumber] )
            && 'Calendar'[QuarterNumber] = EARLIER ( 'Calendar'[QuarterNumber] )
    )
)





//-----------------------------------------------------------------------
//--DAX Measure templates
//-----------------------------------------------------------------------

Sales YTD = CALCULATE (
    [Sales Amount],
    FILTER (
        ALL ( 'Calendar' ),
        'Calendar'[YearNumber] = MAX ( 'Calendar'[YearNumber] )
            && 'Calendar'[Date] <= MAX ( 'Calendar'[Date] )
    )
)

Sales QTD = CALCULATE (
    [Sales Amount],
    FILTER (
        ALL ( 'Calendar' ),
        'Calendar'[YearQuarterNumber] = MAX ( 'Calendar'[YearQuarterNumber] )
            && 'Calendar'[Date] <= MAX ( 'Calendar'[Date] )
    )
)

Sales MTD = CALCULATE (
    [Sales Amount],
    FILTER (
        ALL ( 'Calendar' ),
        'Calendar'[YearMonthNumber] = MAX ( 'Calendar'[YearMonthNumber] )
            && 'Calendar'[Date] <= MAX ( 'Calendar'[Date] )
    )
)

// Moving Annual Total
Sales MAT = CALCULATE ( 
    [Sales Amount],
    FILTER ( 
        ALL ( 'Calendar' ),
        'Calendar'[SequentialDayNumber] > MAX ( 'Calendar'[SequentialDayNumber] ) - 365
             && 'Calendar'[SequentialDayNumber] <= MAX ( 'Calendar'[SequentialDayNumber] )
    ) 
)

// Prior Period Measures
// Note: [YearMonthNumber] must be a sequntial month number, 
// better called [SequentialMonthNumber]
// Add columm to calendar: YearMonthNumber = ('Calendar'[YearNumber] - MIN( 'Calendar'[YearNumber] )) * 12 + 'Calendar'[MonthNumber]

Sales PY=
SUMX ( 
    VALUES ( 'Calendar'[YearMonthNumber] ), 
    IF ( 
        CALCULATE ( 
            COUNTROWS ( 
                VALUES ( 'Calendar'[Date] ) 
            ) 
        ) 
            = CALCULATE ( 
                VALUES ( 'Calendar'[MonthDays] ) 
            ), 
        CALCULATE ( 
            [Sales Amount], 
            ALL ( 'Calendar' ), 
            FILTER ( 
                ALL ( 'Calendar'[YearMonthNumber] ), 
                'Calendar'[YearMonthNumber] 
                    = EARLIER ( 'Calendar'[YearMonthNumber] ) - 12
            ) 
        ), 
        CALCULATE ( 
            [Sales Amount], 
            ALL ( 'Calendar' ), 
            CALCULATETABLE ( 
                VALUES ( 'Calendar'[MonthDayNumber] ) 
            ), 
            FILTER ( 
                ALL ( 'Calendar'[YearMonthNumber] ), 
                'Calendar'[YearMonthNumber] 
                    = EARLIER ( 'Calendar'[YearMonthNumber] ) - 12
            ) 
        ) 
    ) 
)
//this works
Sales PY =
CALCULATE ( SUM ( [Sales Amount] ), PREVIOUSYEAR ( 'Calendar'[Date] ) )


Sales PQ =
SUMX (
    VALUES ( 'Calendar'[YearMonthNumber] ),
    IF (
        CALCULATE (
            COUNTROWS (
                VALUES ( 'Calendar'[Date] )
            )
        )
            = CALCULATE (
                VALUES ( 'Calendar'[MonthDays] )
            ),
        CALCULATE (
            [Sales Amount],
            ALL ( 'Calendar' ),
            FILTER (
                ALL ( 'Calendar'[YearMonthNumber] ),
                'Calendar'[YearMonthNumber] 
                    = EARLIER ( 'Calendar'[YearMonthNumber] ) - 3
            )
        ),
        CALCULATE (
            [Sales Amount],
            ALL ( 'Calendar' ),
            CALCULATETABLE (
                VALUES ( 'Calendar'[MonthDayNumber] )
            ),
            FILTER (
                ALL ( 'Calendar'[YearMonthNumber] ),
                'Calendar'[YearMonthNumber] 
                    = EARLIER ( 'Calendar'[YearMonthNumber] ) - 3
            )
        )
    )
)

Sales PM = 
SUMX (
     VALUES ( 'Calendar'[YearMonthNumber] ),
    IF (
        CALCULATE (
            COUNTROWS (
                VALUES ( 'Calendar'[Date] )
            )
        )
            = CALCULATE (
                VALUES ( 'Calendar'[MonthDays] )
            ),
        CALCULATE (
            [Sales Amount],
            ALL ( 'Calendar' ),
            FILTER (
                ALL ( 'Calendar'[YearMonthNumber] ),
                'Calendar'[YearMonthNumber] 
                    = EARLIER ( 'Calendar'[YearMonthNumber] ) - 1
            )
        ),
        CALCULATE (
            [Sales Amount],
            ALL ( 'Calendar' ),
            CALCULATETABLE (
                VALUES ( 'Calendar'[MonthDayNumber] )
            ),
            FILTER (
                ALL ( 'Calendar'[YearMonthNumber] ),
                'Calendar'[YearMonthNumber] 
                    = EARLIER ( 'Calendar'[YearMonthNumber] ) - 1
            )
        )
    )
)

// Prior Period
Sales PP =
SWITCH (
    TRUE,
    ISFILTERED ( 'Calendar'[Month] ), [Sales PM],
    ISFILTERED ( 'Calendar'[Quarter] ), [Sales PQ],
    ISFILTERED ( 'Calendar'[YearNumber] ), [Sales PY],
    BLANK ()
)

Sales YOY = [Sales Amount] - [Sales PY]

Sales QOQ = [Sales Amount] - [Sales PQ]

Sales MOM = [Sales Amount] - [Sales PM]

Sales POP = [Sales Amount] - [Sales PP]

Sales YOY% =
DIVIDE ( [Sales YOY], [Sales PY] )

Sales QOQ% =
DIVIDE ( [Sales QOQ], [Sales PQ] )

Sales MOM%  =
DIVIDE ( [Sales MOM], [Sales PM] )

Sales POP% =
DIVIDE ( [Sales POP], [Sales PP] )

Sales PYTD = 
CALCULATE (
    [Sales Amount],
    FILTER (
        ALL ( 'Calendar' ),
        'Calendar'[YearNumber] = MAX ( 'Calendar'[YearNumber] ) - 1
            && 'Calendar'[Date] <= MAX ( 'Calendar'[PY Date] )
    )
)



Sales PQTD = 
CALCULATE (
    [Sales Amount],
    FILTER (
        ALL ( 'Calendar' ),
        'Calendar'[YearQuarterNumber] = MAX ( 'Calendar'[YearQuarterNumber] ) - 1
            && 'Calendar'[Date] <= MAX ( 'Calendar'[PQ Date] )
    )
)

Sales PMTD =
CALCULATE (
    [Sales Amount],
    FILTER (
        ALL ( 'Calendar' ),
        'Calendar'[YearMonthNumber] = MAX ( 'Calendar'[YearMonthNumber] ) - 1
            && 'Calendar'[Date] <= MAX ( 'Calendar'[PM Date] )
    )
)

//Previous Year Moving Annual Total
Sales PMAT =
CALCULATE (
    [Sales Amount],
    FILTER (
        ALL ( 'Calendar' ),
        'Calendar'[SequentialDayNumber] > MAX ( 'Calendar'[SequentialDayNumber] ) - 730
             && 'Calendar'[SequentialDayNumber] <= MAX ( 'Calendar'[SequentialDayNumber] ) - 365
    )
)


Sales YOYTD = [Sales YTD] - [Sales PYTD]

Sales QOQTD = [Sales QTD] - [Sales PQTD]

Sales MOMTD = [Sales MTD] - [Sales PMTD]

Sales AOA = [Sales MAT] - [Sales PMAT]



Sales YOYTD% = DIVIDE ( [Sales YOYTD], [Sales PYTD] )

Sales QOQTD% = DIVIDE ( [Sales QOQTD], [Sales PQTD] )

Sales MOMTD% = DIVIDE ( [Sales MOMTD], [Sales PMTD] )
  
Sales AOA% = DIVIDE ( [Sales AOA], [Sales PMAT] )




//---------------------------------------------------------------
//Moving Average
//---------------------------------------------------------------

Sales Amount (30d avg) =
AVERAGEX (
    DATESINPERIOD ( 'Calendar'[Date], LASTDATE ( 'Calendar'[Date] ), -30, DAY ),
    ),
    [Sales Amount] 
)

Sales Amount (90d avg) =
AVERAGEX (
    DATESINPERIOD ( 'Calendar'[Date], LASTDATE ( 'Calendar'[Date] ), -90, DAY ),
    ),
    [Sales Amount] 
)


//https://javierguillen.wordpress.com/2011/09/13/calculating-moving-averages-in-powerpivot-dax/
//verify lookback math

Sales 12 MMA = 
IF(COUNTROWS(VALUES('Calendar'[Month])) = 1, 
    CALCULATE( 
SUM( FactInternetSales[SalesAmount] ) /  COUNTROWS(  VALUES ( 'Calendar'[Month] ) )  , 
            DATESBETWEEN( 
                'Calendar'[FullDateKey], 
                FIRSTDATE(PARALLELPERIOD('Calendar'[FullDateKey], -11, MONTH)), 
                LASTDATE(PARALLELPERIOD('Calendar'[FullDateKey], 0, MONTH)) 
            ), ALL('Calendar') 
    ) 
)

Sales 3 MMA = 
IF(COUNTROWS(VALUES('Calendar'[Month])) = 1, 
    CALCULATE( 
SUM( FactInternetSales[SalesAmount] ) /  COUNTROWS(  VALUES ( 'Calendar'[Month] ) )  , 
            DATESBETWEEN( 
                'Calendar'[FullDateKey], 
                FIRSTDATE(PARALLELPERIOD('Calendar'[FullDateKey], -2, MONTH)), 
                LASTDATE(PARALLELPERIOD('Calendar'[FullDateKey], 0, MONTH)) 
            ), ALL('Calendar') 
    ) 
)